plugins {
    id("com.github.johnrengelman.shadow") version "7.1.0"
    id("io.micronaut.application") version "2.0.6"
}

group = "micronaut.mushop"

repositories {
    mavenCentral()
}

micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("catalogue.*")
    }
}

dependencies {
    annotationProcessor("io.micronaut.micrometer:micronaut-micrometer-annotation")
    annotationProcessor("io.micronaut.data:micronaut-data-processor")
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")

    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut:micronaut-tracing")
    implementation("io.micronaut:micronaut-validation")

    // Metrics - Core
    implementation("io.micronaut:micronaut-management")
    implementation("io.micronaut.micrometer:micronaut-micrometer-core")
    implementation("io.micronaut.micrometer:micronaut-micrometer-registry-prometheus")

    // Metrics - OCI
    runtimeOnly("io.micronaut.oraclecloud:micronaut-oraclecloud-micrometer")

    // Metrics - AWS
    implementation("io.micronaut.micrometer:micronaut-micrometer-registry-cloudwatch")

    // DB - Core
    implementation("io.micronaut.sql:micronaut-jdbc-hikari")
    runtimeOnly("io.micronaut.flyway:micronaut-flyway")
    implementation("io.micronaut.data:micronaut-data-jdbc")

    // DB - OCI ATP
    implementation platform("com.oracle.database.jdbc:ojdbc-bom:21.1.0.0")
    runtimeOnly("com.oracle.database.jdbc:ojdbc8")
    runtimeOnly("io.micronaut.oraclecloud:micronaut-oraclecloud-atp:2.0.2")
    runtimeOnly("com.oracle.oci.sdk:oci-java-sdk-monitoring")

    // DB - AWS RDS
    runtimeOnly 'mysql:mysql-connector-java:8.0.25'

    // AWS - Configuration discovery
    implementation("io.micronaut.aws:micronaut-aws-secretsmanager")
    implementation("io.micronaut.aws:micronaut-aws-parameter-store")

    // AWS - authentication
    implementation("software.amazon.awssdk:sts")

    // Tracing
    implementation("io.opentracing.brave:brave-opentracing")
    implementation("io.swagger.core.v3:swagger-annotations")
    runtimeOnly("io.zipkin.brave:brave-instrumentation-http")
    runtimeOnly("io.zipkin.reporter2:zipkin-reporter")

    runtimeOnly("ch.qos.logback:logback-classic")

    // Caching
    implementation("io.micronaut.cache:micronaut-cache-caffeine")

    testImplementation("org.testcontainers:junit-jupiter")
    testImplementation("org.testcontainers:oracle-xe:1.16.2")
    testImplementation("org.testcontainers:testcontainers:1.16.2")
}

application {
    mainClass.set("catalogue.Application")
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

if (javaBaseImage == "graalvm") {
    dockerfile {
        baseImage = "ghcr.io/graalvm/graalvm-ce:ol8-java11-21.1.0"
    }
}

dockerBuild {
    images = ["iad.ocir.io/cloudnative-devrel/micronaut-showcase/mushop/$project.name-${javaBaseImage}:$project.version"]
}

dockerBuildNative {
  images = ["iad.ocir.io/cloudnative-devrel/micronaut-showcase/mushop/${project.name}-native:$project.version"]
}

dockerfile {
    runCommand("microdnf install strace")
    entryPoint('strace', "java", "-jar", "/home/app/application.jar")
}
